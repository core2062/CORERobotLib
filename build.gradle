plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "3rdparty/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googletest/" + getPlatformPath(targetPlatform) + "/libgtest.a")
                }
            }
            googleTestMain {
                headers.srcDir "3rdparty/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googletest/" + getPlatformPath(targetPlatform) + "/libgtest_main.a")
                }
            }
            googleMock {
                headers.srcDir "3rdparty/googletest/googlemock/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googlemock/" + getPlatformPath(targetPlatform) + "/libgmock.a")
                }
            }
            googleMockMain {
                headers.srcDir "3rdparty/googletest/googlemock/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googlemock/" + getPlatformPath(targetPlatform) + "/libgmock_main.a")
                }
            }
            json {
                headers.srcDir "3rdparty/json/src"
            }
        }
    }
    components { 
        CORERobotLib(NativeLibrarySpec) {
            targetPlatform wpi.platforms.roborio
            sources.cpp {
                source {
                    srcDir "src"
                    include "**/*.cpp"
                    exclude "CORESimulation"
                }
                exportedHeaders {
                    srcDir "src"
                    srcDir "src/WaypointFollower/Swerve"
                    srcDir "src/WaypointFollower/Tank"
                    srcDir "3rdparty/json/src/"
                    srcDir "3rdparty/seasocks/src/main/c"
                }
            }
            binaries.withType(SharedLibraryBinarySpec) {
                buildable = false
            }
            useLibrary(it, "wpilib")
            wpi.deps.vendor.cpp(it)
        }
    }
}

task copyToLibDir(type: Copy) {
    from "build/libs/cORERobotLib/static"
    into "lib"
}

build.finalizedBy copyToLibDir

def getPlatformPath(Platform platform) {
    if (platform.operatingSystem.windows) {
        return "Windows/amd64/"
    } else if (platform.architecture.arm) {
        return "Linux/arm/"
    } else {
        return "Linux/amd64/"
    }
}

