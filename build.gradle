plugins {
    id "cpp"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.22"
}

apply plugin: "cpp"
apply plugin: 'google-test-test-suite'
apply plugin: 'eclipse'

model {
    platforms {
        amd64 {
            architecture 'amd64'
        }
    }
    repositories {
        libs(PrebuiltLibraries) {
            seasocks {
                headers.srcDir "3rdparty/seasocks/src/main/c"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/seasocks/" + getPlatformPath(targetPlatform) + "/libseasocks.a")
                }
            }
            googleTest {
                headers.srcDir "3rdparty/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googletest/" + getPlatformPath(targetPlatform) + "/libgtest.a")
                }
            }
            googleTestMain {
                headers.srcDir "3rdparty/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googletest/" + getPlatformPath(targetPlatform) + "/libgtest_main.a")
                }
            }
            googleMock {
                headers.srcDir "3rdparty/googletest/googlemock/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googlemock/" + getPlatformPath(targetPlatform) + "/libgmock.a")
                }
            }
            googleMockMain {
                headers.srcDir "3rdparty/googletest/googlemock/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("3rdparty/googletest/googlemock/" + getPlatformPath(targetPlatform) + "/libgmock_main.a")
                }
            }
            wpilibrary {
                headers.srcDir "3rdparty/wpilib/include"
            }
            json {
                headers.srcDir "3rdparty/json/src"
            }
        }
    }
    components {
        CORERobotLib(NativeLibrarySpec) {
            targetPlatform 'roborio'
            sources.cpp {
                source {
                    srcDir "src"
                    include "**/*.cpp"
                    exclude "WaypointFollower", "CORESimulation"
                }
                exportedHeaders {
                    srcDir "src"
                }
                lib library: "json", linkage: "api"
                lib library: "seasocks", linkage: "static"
                lib library: "ctre"
                lib library: "navx"
                lib library: 'wpilib'
            }
        }
        CORERobotLibTest(NativeLibrarySpec) {
            targetPlatform "amd64"
            sources.cpp {
                source {
                    srcDirs "src", "test"
                        include "**/*.cpp"
                        exclude "CORESimulation", "COREKinematics", "COREData", "COREDrive", "COREDashboard", "COREDrive/COREEtherDrive.*"
                }
                exportedHeaders {
                    srcDirs "src", "src/CORESimulation"
                }
                binaries.all {
                    lib library: "wpilibrary", linkage: "api"
                    lib library: "json", linkage: "api"
                    lib library: "googleTest", linkage: "static"
                    lib library: "googleTestMain", linkage: "static"
                    lib library: "googleMock", linkage: "static"
                    lib library: "googleMockMain", linkage: "static"
                    cppCompiler.define "NOT_REAL"
			cppCompiler.args "-g3"
                    if (targetPlatform.operatingSystem.linux) {
                        cppCompiler.args '-pthread -g'
                            linker.args '-pthread'
                    }            
                }
            }
        }
        all {
            binaries.withType(SharedLibraryBinarySpec) {
                buildable = false
            }
        }
    }
    testSuites {
        CORERobotLib_Test(GoogleTestTestSuiteSpec) {
            testing $.components.CORERobotLibTest            
        }
    }
    binaries {
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "wpilibrary", linkage: "api"
            lib library: "json", linkage: "api"
            lib library: "googleTest", linkage: "static"
            lib library: "googleTestMain", linkage: "static"
            lib library: "googleMock", linkage: "static"
            lib library: "googleMockMain", linkage: "static"
            cppCompiler.define "NOT_REAL"
			cppCompiler.args "-g"
            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
        all {
            cppCompiler.args "-g3"
        }
    }
}

def getPlatformPath(Platform platform) {
    if (platform.operatingSystem.windows) {
        return "Windows/amd64/"
    } else {
        return "Linux/amd64/"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
